apply plugin: 'com.android.application'

android {
    def versionPropsFile = file('version.properties')
    def versionBuild
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    lintOptions {
        abortOnError false
    }

    buildFeatures {
        dataBinding true
    }

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
    } else {
        throw new FileNotFoundException("Could not read version.properties!")
    }

    ext.autoIncrementBuildNumber = {
        if (versionPropsFile.canRead()) {
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new FileNotFoundException("Could not read version.properties!")
        }
    }

    defaultConfig {
        applicationId 'com.eszdman.photoncamera'
        minSdkVersion 26
        targetSdkVersion 30
        renderscriptTargetApi 24
        renderscriptSupportModeEnabled false
        versionCode versionBuild
        versionName '0.69'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "arm64-v8a"
        }
    }

    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(assembleDebug)) {
            autoIncrementBuildNumber()
        } else if (taskGraph.hasTask(assembleRelease)) {
            autoIncrementBuildNumber()
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            jniDebuggable = false
            zipAlignEnabled = true
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            jniDebuggable = true
            renderscriptDebuggable = true
            zipAlignEnabled = true
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    applicationVariants.all {
        variant -> variant.outputs.all {
            outputFileName = "PhotonCamera-${versionName}${versionBuild}-${variant.name}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'org.jetbrains:annotations:15.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.github.mohammadatif:Animatoo:master'
    implementation 'com.github.ChickenHook:RestrictionBypass:2.2'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.cedarsoft:aparapi:8.7.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation group: 'commons-io', name: 'commons-io', version: '20030203.000550'
    implementation 'rapid.decoder:library:0.3.0'
    implementation 'rapid.decoder:jpeg-decoder:0.3.0'
    implementation 'rapid.decoder:png-decoder:0.3.0'
}